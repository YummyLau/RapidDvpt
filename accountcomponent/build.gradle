if (rootProject.ext.module.openModuleAccount.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

//        //配置buildConfig变量
//        buildConfigField "boolean", "TEST_HOST_API", testHostApi.toBoolean().toString()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        moduleName           : project.getName(),
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.module.openModuleAccount.toBoolean()) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    resourcePrefix "account_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    annotationProcessor deps.arouter.compiler
    annotationProcessor deps.lifecycle.compiler
    annotationProcessor deps.room.compiler

    implementation 'com.sina.weibo.sdk:core:2.0.3:openDefaultRelease@aar'
    implementation project(':componentservice')
}
