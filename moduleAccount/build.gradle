if (buildModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion


    dataBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //配置buildConfig变量
        buildConfigField "boolean", "TEST_HOST_API", testHostApi.toBoolean().toString()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        moduleName           : project.getName(),
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (buildModule.toBoolean()) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    resourcePrefix "account_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jmockit:jmockit:1.36'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    androidTestImplementation "android.arch.persistence.room:testing:1.0.0"

    annotationProcessor rootProject.ext.dependencies["lifecycleCompiler"]
    annotationProcessor rootProject.ext.dependencies["roomCompiler"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    implementation 'com.sina.weibo.sdk:core:2.0.3:openDefaultRelease@aar'
    api project(':library')
    implementation project(':commonRes')
}
